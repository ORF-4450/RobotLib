plugins {
    id "java"
    id "eclipse"
    id "idea"
	id "maven"
    id "jaci.openrio.gradle.GradleRIO" version "2018.03.06"
}

def TEAM = 4450 //Mostly unused
def ROBOT_CLASS = "Team4450.RobotLib.LibraryVersion" //Unused

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", jaci.openrio.gradle.frc.RoboRIO) {
            team = TEAM
        }
    }
    artifacts {
        artifact('frcJava', jaci.openrio.gradle.frc.FRCJavaArtifact) {
            targets << "roborio"
        }
    }
}

// Defining my dependencies. In this case, WPILib (+ friends), CTRE Phoenix (Talon SRX)
// and NavX.
dependencies {
    compileOnly wpilib()
    compileOnly ctre()
    compileOnly navx()
}

// Create Sources Jar
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// Create Javadoc Jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Additional artifacts to publish for JitPack
artifacts {
    archives sourcesJar
    archives javadocJar
}

// When Javadoc is created (./gradlew.bat javadoc) it will be in this directory in the project, docs folder can be configured to auto-publish to GitHub.
javadoc {
    destinationDir = new File("docs")
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.4'
}