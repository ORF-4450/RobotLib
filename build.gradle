plugins {
    id "java-library"
    id "eclipse"
    //id "idea"
    id "edu.wpi.first.GradleRIO" version "2022.1.1-beta-1" 
    id "org.ajoberstar.grgit" version "3.0.0"
    id "maven-publish"
    id "io.github.mosadie.vendorJSON" version "1.0"
}

group = archivesGroup

// Configuration for build task which compiles RobotLib, generates the javadoc and does the install 
// of RobotLib in local maven cache.
// Also used by Jitpack/GitHub Actions.

if (System.getProperty("local")) {
	println("This is a local Compile:")
	version = "local"
}
else {
	println("This is a versioned Compile:")
	version = LibraryVersion
}
                    // Version identifier used for local maven cache and github releases.
					// Should stay as "local". This allows the robot* projects on the
					// same PC as RobotLib to pull from the local cache with the fixed
					// version name "local" and so always be pointing at the current RobotLib
					// version including those under development and not have to be updated
					// each time the robotlib version changes. Release to the world via github
					// is based on actual version numbers and robotLib.json.
					//
					// When starting a new version you can update LibraryVersion.java,
					// overview.html and gradle.properties. DO NOT update robotlib.json. It is
					// maintained by the release github action when you release with github.
					//
					// Build in Eclispe using the Build RobotLib or Build RobotLib (Offline) 
					// run configurations. You can push to github as much as you want.
					//
					// When ready to release, make sure the version number and dates are 
					// correct in the files named above then update README.md. Final push all of your
                    // changes to GitHub. Also check on github Actions tab if the latest commit has a
                    // checkmark, indicating if GitHub was able to build the code successfully.
					//
					// Then on GitHub, create a new release. This will create the release
					// and then GitHub Actions will be triggered to compile the project and
					// add the generated files (artifacts) to the release. Additionally, the release
					// will become available from the Jitpack.io repository. Note that
					// the IF statement above detects when gradle is running on GitHub Actions
					// and sets the version varible to the actual version number (instead
					// of "local") when compiling for release. The release action with update
					// robolib.json to the new version on github. After release action is complete
					// you can fetch from github with tags to get the updated robotlib.json and
					// release tag.
					//
					// When all this is done, other user's robot projects that want the updated
					// RobotLib edit their local RobotLib.json to reflect the new version number 
					// and on next compile, gradle will pull the new library to their PC.
					//
					// If using VSCode, run the Manage Vendor Libraries (online) and it will detect
					// the new RobotLib version and offer to update the VSCode project.
					//
					// Warning: The github release action only works on the master branch. Do not
					// create releases from other branches.
					//
					// Note: the github action files gradleCI.yml and gradleRelease.yml are
					// located in .github\workflows.
					//

println("LibraryVersion=" + LibraryVersion + "; compile version=" + version)
					
// This task updates the vendor json file for this project to the specified version.					
vendorJSON {
    fileName = jsonFileName
    version = LibraryVersion
    addJavaArtifact(archivesGroup, archivesBaseName, LibraryVersion)
}

// This section is to prevent people from accidently deploying this project to a robot. 
deploy {
    for (String task : project.gradle.startParameter.taskNames) {
        if (task == "deploy")
            throw new GradleException("This is a library project! You can't deploy it to a robot!")
    }
}

// Look in local maven cache first, then maven central for dependent libraries.
repositories {
	// This is needed to fix problem with the url below redirecting to an http (insecure) url.
	maven {
        url "https://www.revrobotics.com/content/sw/color-sensor-v3/sdk/maven/"
        allowInsecureProtocol = true
    }

	mavenLocal()
	mavenCentral()
}

// Setup eclipse classpath settings, so the javadoc and sources are available in eclipse
eclipse {
    classpath {
        downloadJavadoc = true
        downloadSources = true
    }
}

// Defining dependencies. In this case, WPILib (+ friends), and some vendor libraries (NavX, CTRE)
dependencies {
    //compile wpi.deps.wpilib()
    //compile wpi.deps.vendor.java()
    //nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    //nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)
    
    implementation wpi.java.deps.wpilib()
    implementation wpi.java.vendor.java()
        
	//implementation wpi.deps.wpilib()
    //nativeZip wpi.deps.wpilibJni(wpi.platforms.roborio)
    //nativeDesktopZip wpi.deps.wpilibJni(wpi.platforms.desktop)

    //implementation wpi.deps.vendor.java()
    //nativeZip wpi.deps.vendor.jni(wpi.platforms.roborio)
    //nativeDesktopZip wpi.deps.vendor.jni(wpi.platforms.desktop)

    // For reference, here are a few more of the easy to add libraries not currently used:
   	//implementation pathfinder() (Jaci's)
   	//implementation openrioMatchData()

   	// Quick guide to keywords:
   	// api is for libraries that are expected to be needed in the main robot project.
   	// implementation is for libraries that will not be needed in the main robot project.
   	// (Both api and implementation keywords will have the library automatically downloaded with RobotLib)
   	// compileOnly expects the dependency to be provided somewhere else. (Ex. by a main robot project)
   	// compile means include in the output library jar (see below).
}

// These next definitions (branch and hash) attempt to find extra information to add to the the manifest of
// the robot program jar file.

// Either the name of the current branch, or an empty string if no git repo is found.
def branch = { ->
    if (grgit == null) { // If there's no git repo.
        return ""
    }

    return grgit.branch.current().getName()
}

// Either the short hash of the last commit or an empty string if no git repo is found.
def hash = { ->
    if (grgit == null) { // If there's no git repo.
        return ""
    }
    
    return grgit.head().abbreviatedId
}

// Setting up RobotLib Jar File. In this case, adding version information to jar manifest.
jar {
    manifest {
       	attributes("LibraryVersion": LibraryVersion)	// from gradle.properties file.
       	attributes("Time": new Date().format("yyyy-MM-dd HH:mm:ss"))
       	attributes("User": System.getProperty("user.name"))
       	attributes("Branch": branch())
       	attributes("Commit": hash())
	}
}

// Create Sources Jar
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// When Javadoc is created (./gradlew.bat javadoc) it will be in this directory in the project folder.
javadoc {
    destinationDir = file("doc")
    title = "RobotLib"
    options.overview = "src/main/resources/overview.html"
    options.addBooleanOption "-no-module-directories", true
    options.addBooleanOption "-frames", true
}

// Create Javadoc Jar
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

// Additional artifacts to publish for JitPack
artifacts {
    archives sourcesJar
    archives javadocJar
}

// Required with gradle 7.1.1. Configures PublishToMavenLocal task. This task is noq required to get our
// jars published to the local maven repository on this PC for consumption by robot projects on this PC.
// The PubishToMavenLocal task is executed in the eclipse .launch files that run this build.
publishing {
    publications {
        mavenJava(MavenPublication) {
	    	artifactId = 'RobotLib'
            from components.java
        }
    }
}